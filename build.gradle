plugins {
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
}

subprojects {
    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    jar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_id}" }
        }
        manifest {
            attributes(['Specification-Title'     : mod_displayName,
                        'Specification-Vendor'    : author,
                        'Specification-Version'   : project.jar.archiveVersion,
                        'Implementation-Title'    : project.name,
                        'Implementation-Version'  : project.jar.archiveVersion,
                        'Implementation-Vendor'   : author,
                        'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                        'Timestamp'               : System.currentTimeMillis(),
                        'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                        'Built-On-Minecraft'      : minecraft_version])
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.getRelease().set(17)
    }

    repositories {
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'BlameJared Maven (JEI / CraftTweaker / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }
        maven {
            name = "Fuzs Mod Resources"
            url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }
    }

    processResources {
        def expandProps = [
                //Project
                "project_group"                : project.group, //Else we target the task's group
                //Mod
                "mod_id"                       : "${mod_id}",
                "mod_version"                  : "${mod_version}",
                "mod_displayName"              : "${mod_displayName}",
                //Minecraft
                "minecraft_version"            : "${minecraft_version}",
                "minecraft_version_range"      : "${minecraft_version_range}",
                //Common
                "author"                       : "${author}",
                "license"                      : "${license}",
                "credits"                      : "${credits}",
                "description"                  : "${mod_description}",
                "displayURL"                   : "${displayURL}",
                "issueTrackerURL"              : "${issueTrackerURL}",
                "updateJSONURL"                : "${updateJSONURL}",
                //Forge
                "forge_version"                : "${forge_version}",
                "forge_loader_version_range"   : "${forge_loader_version_range}",
                //Fabric
                "fabric_version"               : "${fabric_version}",
                "fabric_loader_version"        : "${fabric_loader_version}",
                //NeoForge
                "neoforge_version"             : "${neoforge_version}",
                "neoforge_loader_version_range": "${neoforge_loader_version_range}",
                //Dependencies
                "fcap_version"                 : "${fcap_version}",
                "fcap_version_range"           : "${fcap_version_range}"]
        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', '*.mixins.json']) {
            expand expandProps
        }
        inputs.properties(expandProps)
    }
}